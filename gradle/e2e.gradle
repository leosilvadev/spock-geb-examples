import org.apache.tools.ant.taskdefs.condition.Os

apply from: "gradle/drivers.gradle"

def domains = ['de', 'com']
def browsers = ['chrome', 'firefox', 'phantomJs']

browsers.each { browser ->
    domains.each { domain ->
        def name = "${browser}${domain.capitalize()}"
        task "${browser}Test${domain.capitalize()}"(type: Test) {
            reports {
                html.destination = reporting.file("$name/tests")
                junitXml.destination = file("$buildDir/test-results/$name")
            }

            outputs.upToDateWhen { false }

            systemProperty 'geb.domain', domain
            systemProperty 'geb.build.reportsDir', reporting.file("$name/geb")
            systemProperty 'geb.env', browser

            if (browser == 'chrome') {
                dependsOn installChromeDriver
                systemProperty "webdriver.chrome.driver", "$installChromeDriver.outputs.files.singleFile/chromedriver"
            }

            if (browser == 'phantomJs') {
                def phantomJsFilename = Os.isFamily(Os.FAMILY_WINDOWS) ? "phantomjs.exe" : "bin/phantomjs"
                dependsOn installPhantomJsDriver
                systemProperty "phantomjs.binary.path", new File(installPhantomJsDriver.outputs.files.singleFile, phantomJsFilename).absolutePath
            }
        }
    }
}
